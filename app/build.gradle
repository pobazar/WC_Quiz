apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'

apply from: "$rootDir/config.gradle"
apply from: "$rootDir/deps.gradle"

def releasePropertiesFile = rootProject.file("release_keystore.properties")
def releaseProperties = new Properties()
releaseProperties.load(new FileInputStream(releasePropertiesFile))

android {
    signingConfigs {
        release {
            storeFile file(releaseProperties['storeFile'])
            storePassword releaseProperties['storePassword']
            keyAlias releaseProperties['keyAlias']
            keyPassword releaseProperties['keyPassword']
        }
    }

    defaultConfig {
        applicationId "msk.pobazar.wcquiz"
        versionCode config.versionCode
        versionName config.versionName
        minSdkVersion config.minSdkVersion
        targetSdkVersion config.compileSdkVersion
        compileSdkVersion config.compileSdkVersion

        setProperty("archivesBaseName", "WC_quiz_$versionName($versionCode)")
    }

    buildTypes {
        debug {}
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }

    compileOptions {
        sourceCompatibility = '1.8'
        targetCompatibility = '1.8'
    }
}

dependencies {
    implementation project(':core')
    implementation project(':domain')
    implementation project(':navigation')

    implementation project(':data_remote')
    implementation project(':data_local')
    implementation project(':data_device')

    implementation project(':storage_paper')
    implementation project(':database_firebase')
    implementation project(':adMob')

    implementation project(':feature_splash')
    implementation project(':feature_menu')
    implementation project(':feature_game')
    implementation project(':feature_result')
    implementation project(':feature_rating')
    implementation project(':feature_mods')

    api deps.google.gsm
    api deps.google.firebase.core
    api deps.google.firebase.analytics
    api deps.google.firebase.messaging
    api deps.google.firebase.appindexing
    api deps.google.firebase.perfomance

    api deps.moxyX.android
    api deps.moxyX.core
    api deps.moxyX.material
    kapt deps.moxyX.compiler

    api(deps.glide.core) {
        exclude group: "com.android.support"
    }

    api deps.kotlin

    api deps.google.gson

    api deps.rx.java
    api deps.rx.android
    api deps.rx.kotlin

    api deps.timber
    api deps.appMetrica.mobmetricalib
    api deps.appMetrica.installreferrer

    api deps.toothpick.runtime
    kapt deps.toothpick.compiler
}

apply plugin: 'com.google.gms.google-services'
